/*
 * ld.script for compressed kernel support of MIPS
 *
 * Copyright (C) 2009 Lemote Inc.
 * Author: Wu Zhangjin <wuzhanjing@gmail.com>
 * Copyright (C) 2010 "Wu Zhangjin" <wuzhanjing@gmail.com>
 */

OUTPUT_ARCH(mips)
ENTRY(start)
SECTIONS
{
	/* Text and read-only data */
	/* . = VMLINUZ_LOAD_ADDRESS; */
	.text : {
		*(.text)
		*(.text.[A-Za-z0-9_$^]*)
		*(.rodata)
		*(.rodata.[A-Za-z0-9_$^]*)
		/* Put the compressed image here */
		__image_begin = .;
		*(.image)
		__image_end = .;
		. = ALIGN(16);
	}

	/* BSS */
	.bss : {
		__bss_begin = .;
		*(.bss)
		*(.bss.[A-Za-z0-9_$^]*)
		. = ALIGN(16);
		__bss_end = .;
	}

	/* Sections to be discarded */
	/DISCARD/ : {
		*(.MIPS.options)
		*(.options)
		*(.pdr)
		*(.reginfo)
		*(.comment)
		*(.note)
		*(.gnu.attributes)
		*(.data)
	}
}
